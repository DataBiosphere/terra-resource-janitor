plugins {
    id 'idea'
    id 'jacoco'
    id 'java'

    id 'com.diffplug.spotless' version '6.9.1'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'com.google.cloud.tools.jib' version '3.2.1'
    id 'de.undercouch.download' version '5.1.0'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'org.hidetake.swagger.generator' version '2.19.2'
    id 'org.springframework.boot' version '2.7.2'
}

allprojects {
    group = 'bio.terra'
    version = gradle.janitorVersion
    ext {
        artifactGroup = "${group}.janitor"
        swaggerOutputDir = "${buildDir}/generated"
        resourceDir = "${rootDir}/src/main/resources"
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    // specifying versions for dependencies, not adding the dependencies themselves (yet)
    apply plugin: 'io.spring.dependency-management'
    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
        dependencies {
            dependency group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.2'
            dependency group: 'io.swagger.codegen.v3', name: 'swagger-codegen-cli', version: '3.0.35'
            dependency group: 'io.swagger', name: 'swagger-annotations', version: '1.6.6'
        }
    }

    apply plugin: 'idea'
    idea {
        module {
            generatedSourceDirs = [file("${swaggerOutputDir}/src/main/java")]
            downloadJavadoc = true
        }
    }
}

// If true, search local repository (~/.m2/repository/) first for dependencies.
def useMavenLocal = false
repositories {
    if (useMavenLocal) {
        mavenLocal() // must be listed first to take effect
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }

}

dependencies {
    // Terra deps - we get Stairway via TCL
    implementation group: 'bio.terra', name: 'terra-common-lib', version: '0.0.67-SNAPSHOT'
    implementation group: 'bio.terra', name: 'terra-cloud-resource-lib', version: '1.2.0-SNAPSHOT'
    implementation group: 'bio.terra', name: 'workspace-manager-client', version: '0.254.345-SNAPSHOT'

    // hk2 is required to use WSM client, but not correctly exposed by the client
    implementation group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '3.0.6'

    // Versioned direct deps
    implementation group: 'com.google.auto.value', name: 'auto-value-annotations', version: '1.9'
    implementation group: 'com.google.cloud', name: 'google-cloud-pubsub', version: '1.120.11'
    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    implementation group: 'io.opencensus', name: 'opencensus-exporter-stats-stackdriver', version: '0.31.1'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    implementation group: 'org.liquibase', name: 'liquibase-core', version: '4.15.0'
    implementation group: 'org.webjars', name: 'webjars-locator-core', version: '0.52'
    runtimeOnly group: 'org.postgresql', name: 'postgresql', version: '42.4.1'

    // Deps whose versions are controlled by Spring
    implementation group: 'org.apache.commons', name: 'commons-dbcp2'
    implementation group: 'org.apache.commons', name: 'commons-lang3'
    implementation group: 'org.apache.commons', name: 'commons-pool2'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jdbc'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'

    // Swagger deps
    implementation group: 'io.swagger', name: 'swagger-annotations', version: '1.6.6'
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.2'
    runtimeOnly group: 'org.webjars.npm', name: 'swagger-ui-dist', version: '3.36.2'
    swaggerCodegen group: 'io.swagger.codegen.v3', name: 'swagger-codegen-cli', version: '3.0.34'

    // Test deps
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    annotationProcessor group: 'com.google.auto.value', name: 'auto-value', version: '1.9'
    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: '2.7.2'
}

// for scans
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

apply from: "$rootDir/gradle/dependency-locking.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/javadoc.gradle"
apply from: "$rootDir/gradle/jib.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/swagger-server.gradle"
apply from: "$rootDir/gradle/test.gradle"

tasks.withType(com.diffplug.gradle.spotless.SpotlessTask) {
    it.dependsOn(swaggerSources.server.code)
}

sourceSets.main.java.srcDir "${swaggerOutputDir}/src/main/java"

// TODO - update deps
