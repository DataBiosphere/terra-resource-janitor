// This is the path to the default Google service account for the janitor service to run as.
def googleCredentialsFile = "${rootDir}/src/test/resources/rendered/sa-account.json"

test {
    useJUnitPlatform()
}

tasks.withType(Test) {
    environment = [
            'GOOGLE_APPLICATION_CREDENTIALS': "${googleCredentialsFile}"
    ]
    testLogging {
        events = ['passed', 'failed', 'skipped']
        // Causes the correct line to be reported on an exception.
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        reports {
            // Write XML file (used by CircleCI, Jenkins, etc) to build/test-results/*
            junitXml.required = true
            // Write human-readable test report to build/reports/tests/*
            html.required = true
        }
    }
}

task unitTest(type: Test) {
    useJUnitPlatform {
        includeTags 'unit'
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform {
        includeTags 'integration'
    }
    // Force tests to always be re-run, since integration tests involve communicating with external
    // resources.
    outputs.upToDateWhen { false }
}
