plugins {
    id 'java-library'
    id 'maven-publish'

    id 'com.google.cloud.artifactregistry.gradle-plugin' version '2.2.5' apply false
    id 'org.hidetake.swagger.generator' version '2.19.2'
}

// Only apply the Artifact Registry plugin if a publish task is being run. This avoids requiring gcloud to be
// installed and authenticated in CI jobs that don't publish (e.g., linter, tests, static analysis).
def isPublishing = gradle.startParameter.taskNames.any { it.toLowerCase().contains('publish') }
if (isPublishing) {
    apply plugin: 'com.google.cloud.artifactregistry.gradle-plugin'
}

repositories {
    mavenCentral()
}

dependencies {
    // Swagger deps, versions controlled by dependencyManagement in root project
    implementation group: 'org.glassfish.jersey.core', name: 'jersey-client'
    implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson'
    implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations'
    swaggerCodegen group: 'io.swagger.codegen.v3', name: 'swagger-codegen-cli'
}

def gradleIncDir= "$rootDir/gradle"
apply from: "$gradleIncDir/dependency-locking.gradle"
apply from: "$gradleIncDir/publishing.gradle"
apply from: "$gradleIncDir/swagger-client.gradle"

sourceSets.main.java.srcDir "${swaggerOutputDir}/src/main/java"
